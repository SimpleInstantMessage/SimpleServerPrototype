plugins {
  id "java"
  id "groovy"
  id "com.google.protobuf" version "0.7.6"
  id "application"
}

group = 'gq.baijie.tryit.proto'

mainClassName = "${group}.MainG"

def protobufVersion = '3.0.0-beta-2'

// In this section you declare where to find the dependencies of your project
repositories {
    // Use 'jcenter' for resolving your dependencies.
    // You can declare any Maven/Ivy/file repository here.
    jcenter()
}


configurations.all {
  resolutionStrategy.eachDependency { DependencyResolveDetails details ->
    if (details.requested.name == 'groovy-all') {
      //prefer 'groovy' over 'groovy-all':
      details.useTarget group: details.requested.group, name: 'groovy', version: details.requested.version
    }
  }
}

// In this section you declare the dependencies for your production and test code
dependencies {
    // The production code uses the SLF4J logging API at compile time
    def slf4jVersion = '1.7.21'
    compile "org.slf4j:slf4j-api:$slf4jVersion"
    compile "org.slf4j:slf4j-simple:$slf4jVersion"
    compile "com.google.protobuf:protobuf-java:$protobufVersion"
    compile 'org.codehaus.groovy:groovy:2.4.6'
    compile "io.vertx:vertx-core:3.2.1"
    compile "io.vertx:vertx-lang-groovy:3.2.1"
    compile 'com.squareup.okhttp3:okhttp:3.2.0'

    // Declare the dependency for your favourite test framework you want to use in your tests.
    // TestNG is also supported by the Gradle Test task. Just change the
    // testCompile dependency to testCompile 'org.testng:testng:6.8.1' and add
    // 'test.useTestNG()' to your build script.
    testCompile 'junit:junit:4.12'
}


  tasks.withType(Javadoc).each { task ->
    task.options.with {
      locale 'en_US'
      charSet 'UTF-8'
      docEncoding 'UTF-8'
      links (/*"https://jsr-305.googlecode.com/svn/trunk/javadoc/",*/
            "https://docs.oracle.com/javase/${task.project.sourceCompatibility.majorVersion}/docs/api/")
    }
  }

protobuf {

  // Configure the protoc executable
  protoc {
    // Download from repositories
    artifact = "com.google.protobuf:protoc:$protobufVersion"
  }

}




apply plugin: 'maven-publish'

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java

      artifact sourcesJar
      artifact javadocJar
    }
  }
}

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}
